#include <WiFi.h>
#include <HTTPClient.h>
#include <time.h>

// Definições para o calculo da tempertura
#define ADC_RESOLUTION 4095.0 // Resolução do ADC (0-4095 para um ADC de 12 bits)
#define V_REF 3.3             // Tensão de referência do ADC (em volts)
#define R_FIXED 10000.0       // Valor do resistor fixo (em ohms)
#define BETA 3950.0           // Valor Beta do NTC (em Kelvin)
#define T0 298.15             // Temperatura de referência em Kelvin (25°C)
#define R0 10000.0            // Resistência do NTC a 25°C (10kΩ)

// Pinos analogicos
const int pinLuz = 32;  
const int pinTemp = 35;

const char *ssid = "M201";
const char *password = "apartamento201";

//URL da API
String apiUrl = "http://192.168.0.18:8080/condicaoClimatica";

void setup() {
  Serial.begin(115200);
  WiFi.begin(ssid, password);
  pinMode(2, OUTPUT);  // set the LED pin mode

  delay(10);

  // We start by connecting to a WiFi network

  Serial.print("Connecting to ");
  Serial.println(ssid);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("WiFi connected.");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void ST_conection() {
  if(WiFi.status() == WL_CONNECTED){
    digitalWrite(2, HIGH);
    
  } else {
    digitalWrite(2, LOW);
    Serial.println("Erro: Wi-Fi desconectado");
  }
}

const char* getDateTimeBr() {
    static char buffer[80]; // Buffer para armazenar a data e hora formatadas
    struct tm *p;
    time_t seconds;

    // Obtém o tempo atual em segundos desde o Epoch
    time(&seconds);

    // Subtrai 3 horas (3 * 3600 segundos)
    seconds -= 3 * 3600;

    // Converte o tempo ajustado para a estrutura tm
    p = localtime(&seconds);

    // Formata a data e a hora
    strftime(buffer, sizeof(buffer), "%d/%m/%Y %H:%M:%S", p);

    // Retorna o buffer contendo a data formatada
    return buffer;
}

void get_api() {
  if (WiFi.status() == WL_CONNECTED) { // Verifica se está conectado antes de fazer a requisição
    HTTPClient http;
    http.begin(apiUrl);
    
    // Envia a requisição GET
    int httpCode = http.GET();
    
    // Verifica o status da resposta
    if (httpCode > 0) {
      // Se o código for 200, exibe o JSON no console
      if (httpCode == HTTP_CODE_OK) {
        String payload = http.getString();
        Serial.println("Resposta da API:");
        Serial.println(payload);
      }
    } else {
      Serial.printf("Erro ao realizar a requisição: %s\n", http.errorToString(httpCode).c_str());
    }
    http.end();
  } else {
    Serial.println("Wi-Fi desconectado, não foi possível fazer a requisição GET");
  }
}

void post_api(double temperatura, int luminosidade) {
  if (WiFi.status() == WL_CONNECTED) { // Verifica se está conectado antes de fazer a requisição
    HTTPClient http;
    http.begin(apiUrl); // Inicia a conexão com a URL

    // Define o tipo de conteúdo (JSON)
    http.addHeader("Content-Type", "application/json");

    // Dados a serem enviados no corpo da requisição
    String postData = "{\"temperatura\":" + String(temperatura, 2) + ", \"luminosidade\":" + String(luminosidade) + "}";

    // Envia a requisição POST
    int httpCode = http.POST(postData);

    // Verifica a resposta da requisição
    if (httpCode > 0) {
      // Se o código for 200 ou 201, exibe a resposta no console
      String payload = http.getString();
      Serial.println("Resposta da API:");
      Serial.println(payload);
    } else {
      Serial.printf("Erro ao realizar a requisição: %s\n", http.errorToString(httpCode).c_str());
    }

    http.end(); // Fecha a conexão HTTP
  } else {
    Serial.println("Wi-Fi desconectado, não foi possível fazer a requisição POST");
  }
}

double getTemperatureCelsius(int adcValue) {

  // Converte o valor ADC para a tensão
  double Vout = (adcValue / ADC_RESOLUTION) * V_REF;
  
  // Calcula a resistência do NTC
  double R_ntc = R_FIXED * (V_REF / Vout - 1);
  
  // Aplica a equação Beta para calcular a temperatura em Kelvin
  double tempK = 1 / (1 / T0 + (1 / BETA) * log(R_ntc / R0));
  
  // Converte Kelvin para Celsius
  double tempC = tempK - 273.15;
  
  return tempC;
}

int getPercentLight(int ldrValue) {
  int percentLight = (ldrValue * 100) / ADC_RESOLUTION;

  return percentLight;
}

void loop() {
  
  // Recebendo o valor analogicos dos sensores
  int luzValue = analogRead(pinLuz);
  int tempValue = analogRead(pinTemp);

  //Verifica a conexão WIFI
  ST_conection();

  // Post
  post_api(getTemperatureCelsius(tempValue), getPercentLight(luzValue));

  // GET (findAll da API)
  get_api();

  delay(600000);  // Atraso de 10 segundos (10000 ms)
}